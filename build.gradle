plugins {
	id 'application'
	id 'java'
	id 'groovy'
	id 'idea'
	id 'com.github.onslip.gradle-one-jar' version '1.1.0'
	id 'com.diffplug.spotless' version '6.13.0'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

group = 'org.codemetrics4j'

application {
	mainClass = 'org.codemetrics4j.executive.CommandLineExecutive'
}

dependencies {
	// General language sugar
	implementation libs.guava
	implementation libs.commons.lang3

	// Libraries
	implementation(libs.jscience) {
		exclude group: 'org.javolution'
	}
	implementation libs.pcollections
	implementation libs.commons.io
	implementation libs.commons.cli
	implementation libs.javaparser
	implementation libs.slf4j

	// Testing
	testImplementation libs.groovy
	testImplementation libs.spock
	testImplementation libs.mockito
	testImplementation libs.javafaker
	testImplementation libs.jgit
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
		palantirJavaFormat()
		formatAnnotations()
	}
	groovy {
		importOrder()
		excludeJava()
		greclipse()
	}
	groovyGradle {
		target '*.gradle' // default target of groovyGradle
		greclipse()
	}
}

tasks.withType(Test) {
	useJUnitPlatform {
		if (!project.hasProperty("runFunctionalTests")) {
			excludeTags "functional"
		}
		if (!project.hasProperty("runExternalTests")) {
			excludeTags "external"
		}
	}
}
